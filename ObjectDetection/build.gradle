plugins {
    id 'com.android.application'
}

android {
    namespace 'com.quicinc.objectdetection'
    compileSdk Integer.parseInt(project.properties['androidCompileSDK'])

    defaultConfig {
        applicationId "com.quicinc.objectdetection"
        minSdk Integer.parseInt(project.properties['androidMinSDK'])
        targetSdk Integer.parseInt(project.properties['androidTargetSDK'])
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resValue( "string", "tfLiteModelAsset", project.properties['objectdetection_tfLiteModelAsset'])
        resValue( "string", "tfLiteLabelsAsset", project.properties['objectdetection_tfLiteLabelsAsset'])
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.valueOf("VERSION_" + project.properties['javaSourceCompatibilityVersion'])
        targetCompatibility JavaVersion.valueOf("VERSION_" + project.properties['javaTargetCompatibilityVersion'])
    }

    preBuild.doFirst {
        if (!file("./src/main/assets/" + project.properties["objectdetection_tfLiteModelAsset"]).exists()) {
            throw new RuntimeException(project.properties["missingModelErrorMsg"])
        }

        if (!file("./src/main/assets/" + project.properties["objectdetection_tfLiteLabelsAsset"]).exists()) {
            throw new RuntimeException(project.properties["missingModelErrorMsg"])
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.camera:camera-core:1.3.0'
    implementation 'androidx.camera:camera-camera2:1.3.0'
    implementation 'androidx.camera:camera-lifecycle:1.3.0'
    implementation 'androidx.camera:camera-view:1.3.0'

    api 'org.tensorflow:tensorflow-lite:2.16.1'
    api 'org.tensorflow:tensorflow-lite-support:0.4.4'
    implementation 'org.tensorflow:tensorflow-lite-gpu:2.16.1'
    implementation 'org.tensorflow:tensorflow-lite-gpu-api:2.16.1'
    implementation 'org.tensorflow:tensorflow-lite-gpu-delegate-plugin:0.4.4'
    implementation "com.qualcomm.qti:qnn-runtime:$qnnVersion"
    implementation "com.qualcomm.qti:qnn-litert-delegate:$qnnVersion"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

if (System.getProperty("user.dir") != project.rootDir.path) {
    throw new RuntimeException("This project should be opened from the root directory, NOT the ObjectDetection directory.")
}